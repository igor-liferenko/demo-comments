mode_setup;
font_size=10pt#;

ht# := 7.7pt#;
wd# := ht#;
penht# := ht#/10;
penwd# := penht#;
thin_weight := 2;                          % factor to increase thin pen stroke
thin# := (penwd#/6.5cosd(45))*thin_weight; % pen stroke width at 135-deg
thick# := (6.25thin#)/thin_weight;         % pen stroke width at 45-deg

define_whole_pixels(ht);
define_pixels(wd,penht,penwd);
define_blacker_pixels(thin,thick);

smoothing := 0;

pen_rot := 135;

pickup pencircle xscaled thick yscaled thin rotated (pen_rot);
main_pen := savepen;

capsule_def (squarepen) makepen (unitsquare shifted (-.5,-.5) scaled thick rotated 135);

capsule_def (penoffsquare) makepen (unitsquare shifted (0,-.5));

pickup penoffsquare xscaled (.8thick) yscaled (1.4thin) rotated pen_rot;
mpen135 := savepen;

pickup penoffsquare xscaled (.8thick) yscaled (1.4thin) rotated (pen_rot-180);
mpen315 := savepen;

pickup penoffsquare xscaled (.8thin) yscaled (1.4thick) rotated (pen_rot+90);
mpen225 := savepen;

pickup penoffsquare xscaled (.8thin) yscaled (1.4thick) rotated (pen_rot-90);
mpen45 := savepen;

def bedraw expr p =
  picture savedpicture; savedpicture = currentpicture; clearit;
  pickup main_pen;
  draw p;
  cullit;
  trim (point 0 of p, 180+angle direction 0 of p);
  trim (point infinity of p, angle direction infinity of p);
  cullit;
  addto currentpicture also savedpicture
enddef;

vardef trim (expr z, theta) =
    if (theta <= 0) : alpha := (theta + 360);
    else : alpha := theta;
    fi
    if ((alpha < 120) or (alpha > 330)) : pickup mpen45;
    elseif ((alpha >= 120) and (alpha <= 150)) : pickup mpen135;
    elseif ((alpha >= 300) and (alpha <= 330)) : pickup mpen315;
    else : pickup mpen225;
    fi
  undrawdot z; % drawdot z;
enddef;

beginchar("?",wd#,.5penht#+ht#,.5penht#);
  bedraw (1/8wd,21/32ht)..{dir0}(5/16wd,3/4ht)..{dir225}(7/16wd,7/16ht)..(5/16wd,3/8ht);
  bedraw (7/16wd,7/16ht)--(1/2wd,3/8ht){dir315}..{dir180}(3/8wd,0)..{dir135}(1/8wd,1/8ht)--(0,1/4ht);
  bedraw (7/16wd,7/16ht){dir315}..(11/16wd,3/8ht)..{dir0}(27/32wd,15/32ht)..{dir270}(wd,5/16ht);
  bedraw (wd,5/16ht){dir270}..{dir180}(27/32wd,5/32ht)..{dir135}(11/16wd,1/4ht);
  bedraw (1/2wd,7/8ht){dir315}..{dir45}(wd,7/8ht);
  pickup squarepen; drawdot(3/4wd,ht);
endchar;
end;
